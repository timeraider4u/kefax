grammar at.jku.weiner.c.cmdarguments.CmdArgs 
//with org.eclipse.xtext.common.Terminals
//hidden()
hidden(LINE_COMMENT)

generate cmdArgs "http://www.jku.at/weiner/c/cmdarguments/CmdArgs"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	{Model}
	(lines+=CmdLine (NEWLINE lines+=CmdLine)* )?
	NEWLINE?
;

CmdLine:
	{CmdLine}
	(arguments+=Argument (WS+ arguments+=Argument)*)
;

Argument:
	{Argument}
	(
		(Define macro=Macro)
	|
		(incDir?=IncDir useIncDir=PathCmd)
	|
		(incSys?=IncSys WS+ useIncDir=PathCmd)
	|
		(nostdinc?=NoStdInc)
	|
		(Include WS+ include=PathCmd)
	|
		(Output WS+ out=PathCmd)
	|
		(Lang WS+ lang=PathCmd)
	|
		(SKW_MINUS (option=Option)?)
	|
		in=Option
	)
;

Define:
	KW_DEFINE
;

IncDir:
	KW_INCDIR
;

IncSys:
	SKW_MINUS KW_INCSYS
;

NoStdInc:
	SKW_MINUS KW_NOSTDINC
;

Include:
	SKW_MINUS KW_INCLUDE
;

Output:
	KW_OUTPUT
;

Lang:
	KW_LANG
;

Macro returns Macro:
	(
		SimpleMacro
	|	ObjectMacro
	|	FunctionMacro
	)
;

SimpleMacro returns Macro:
	{SimpleMacro}
	name=Identifier
;

ObjectMacro returns Macro:
	{ObjectMacro}
	name=Identifier SKW_ASSIGN
	value=MyCode
;

FunctionMacro returns Macro:
	{FunctionMacro}
	name=Identifier
	SKW_LEFTPAREN (params+=Identifier (SKW_COMMA params+=Identifier)*)? 
	SKW_RIGHTPAREN SKW_ASSIGN
	value=MyCode
;

PathCmd:
	path=Path
	//name=Identifier
;

Identifier: ID;

MyCode:
	MyCodeChar+
;
MyCodeChar:
	PathChar | SKW_LEFTPAREN | SKW_RIGHTPAREN
;
Path: 
	PathChar+
;
PathChar:
	OptionChar | NonOptionChar
;

Option:
	NonOptionChar (OptionChar | NonOptionChar)*
;

NonOptionChar:
	ID | INT | STRING | SKW_ASSIGN | SKW_COMMA | ANY_OTHER
;

OptionChar:
	SKW_MINUS | KW_DEFINE | KW_INCDIR | KW_NOSTDINC | KW_INCSYS | KW_INCLUDE | KW_LANG | KW_OUTPUT
;

terminal SKW_MINUS: '-';
terminal SKW_ASSIGN: '=';
terminal SKW_LEFTPAREN: '(';
terminal SKW_RIGHTPAREN: ')';
terminal SKW_COMMA: ',';

//terminal HASH: '#';
terminal KW_DEFINE: SKW_MINUS 'D';
terminal KW_INCDIR: SKW_MINUS 'I';
terminal KW_OUTPUT: SKW_MINUS 'o';
terminal KW_LANG: SKW_MINUS 'x';
terminal KW_NOSTDINC: 'nostdinc';
terminal KW_INCSYS: 'isystem';
terminal KW_INCLUDE: 'include';

terminal ID: LETTER (LETTER|DIGIT)*;

terminal fragment LETTER:
		'$'
	|	'A'..'Z'
	|	'a'..'z'
	|	'_'
;

terminal INT: DIGIT+;
terminal fragment DIGIT: ('0'..'9');

terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
;

terminal NEWLINE: '\n' | '\r';
terminal WS: (' '|'\t');

terminal LINE_COMMENT: '//' -> NEWLINE;

terminal ANY_OTHER: .;
//terminal END: EOF;
