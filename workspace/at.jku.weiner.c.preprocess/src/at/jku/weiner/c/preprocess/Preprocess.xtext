/*
 [The "BSD licence"]
 Copyright (c) 2013 Sam Harwell
 Copyright (c) 2015 Harald Weiner
 All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:
 1. Redistributions of source code must retain the above copyright
    notice, this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright
    notice, this list of conditions and the following disclaimer in the
    documentation and/or other materials provided with the distribution.
 3. The name of the author may not be used to endorse or promote products
    derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
grammar at.jku.weiner.c.preprocess.Preprocess
with at.jku.weiner.c.common.Common
hidden(BLOCK_COMMENT,LINE_COMMENT,LINEBREAK)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate preprocess "http://www.jku.at/weiner/c/preprocess/Preprocess"

Model:
	{Model}
	(units+=TranslationUnit);
	
TranslationUnit:
	{TranslationUnit}
	group=GroupOpt
;

GroupOpt:
	{GroupOpt}
	(
		lines+=PreprocessorDirectives
		|
		lines+=NewLineLine
		|	
		lines+=Code
	)*
;

PreprocessorDirectives returns SourceCodeLine:
	{PreprocessorDirectives}
	WS* HASH WS*
	(
			directive=IncludeDirective
		|	directive=DefineDirective
		|	directive=ErrorDirective
		|	directive=WarningDirective
		|	directive=UnDefineDirective
		|	directive=ConditionalDirective
		|	directive=PragmaDirective
		|	directive=NullDirective
	)
	NEWLINE
;

IncludeDirective:
	{IncludeDirective}
	INCLUDE WS+ string=MyCodeLine
;

DefineDirective:
		DefineObjectMacro
	|	DefineFunctionLikeMacro
;

DefineObjectMacro returns DefineDirective:
	{DefineObjectMacro}
	DEFINE WS+ id=ID
		(WS+ string=MyDefineLine)?
;

DefineFunctionLikeMacro returns DefineDirective:
	{DefineFunctionLikeMacro}
	DEFINE WS+ id=ID 
	SKW_LEFTPAREN WS*
		(list=IdentifierList)?
	SKW_RIGHTPAREN
	WS+
	string=MyDefineLine 
;

IdentifierList:
	{IdentifierList}
	id+=ID (SKW_COMMA WS* id+=ID)*
;

ErrorDirective:
	{ErrorDirective}
	ERROR WS+ msg=MyCodeLine
;

WarningDirective:
	{WarningDirective}
	WARNING WS+ msg=MyCodeLine
;

UnDefineDirective:
	{UnDefineDirective}
	UNDEF WS+ id=ID
;

ConditionalDirective:
	{ConditionalDirective}
	(conditional=
		(		IfConditional	
			|	IfDefConditional
			|	IfNotDefConditional
		)
	)
	(elifs+=ElIfConditional)*
	(else=ElseConditional)?
	WS* HASH WS* ENDIF WS*
;

IfConditional:
	{IfConditional}
	IF WS+ expression=Common::ConstantExpression NEWLINE
		group=GroupOpt
;

IfDefConditional:
	{IfDefConditional}
	IFDEF WS+ id=ID NEWLINE
		group=GroupOpt
;

IfNotDefConditional:
	{IfNotDefConditional}
	IFNOTDEF WS+ id=ID NEWLINE
		group=GroupOpt
;

ElIfConditional:
	{ElIfConditional}
	(WS* HASH WS* ELIF) WS+ expression=Common::ConstantExpression NEWLINE
		group=GroupOpt
;

ElseConditional:
	{ElseConditional}
	WS* HASH WS* ELSE NEWLINE
		group=GroupOpt
;

PragmaDirective:
	{PragmaDirective}
	PRAGMA
;

NullDirective:
	{NullDirective}
;

NewLineLine returns SourceCodeLine:
	{NewLineLine}
	NEWLINE
;

Code returns SourceCodeLine:
	{Code}
	code=MyCodeLineExtended
	NEWLINE
;

MyCode returns ecore::EString:
	ID|SPECIAL|INCLUDE|DEFINE|ERROR|PRAGMA
	|IFDEF|ENDIF|IFNOTDEF|IF|DEFINED
	|SpecialKeywords|Constant
;

MyDefineLine returns ecore::EString:
	(MyCode|WS|HASH)*
;

MyCodeLine returns ecore::EString:
	(MyCode|WS)+
;

MyCodeLineExtended returns ecore::EString:
	WS* (MyCode (MyCodeLine|HASH)*)?
;

// expressions
PrimaryExpression returns Expression:
	{PrimaryExpression}
	(
		(defined?=DEFINED)? id=ID
	|	(defined?=DEFINED)? SKW_LEFTPAREN id=ID SKW_RIGHTPAREN
	|	const=Constant
	//|	string+=STRING_LITERAL+ // reachable over Constant!
	|	SKW_LEFTPAREN expr=Expression SKW_RIGHTPAREN
	)
;

Constant returns ecore::EString:
	(
		HEX_LITERAL
	|	OCTAL_LITERAL
	|	DECIMAL_LITERAL
	|	CHAR_LITERAL
	|	STRING_LITERAL
	|	FLOAT_LITERAL
	|	BIN_LITERAL
	)
;

// lexer tokens
terminal HASH: '#';
terminal INCLUDE: 'include';
terminal DEFINE: 'define';
terminal ERROR: 'error';
terminal WARNING: 'warning';
terminal UNDEF: 'undef';
terminal IF: 'if';
terminal DEFINED: 'defined';
terminal IFDEF: 'ifdef';
terminal IFNOTDEF: 'ifndef';
terminal ELIF: 'elif';
terminal ELSE: 'else';
terminal ENDIF: 'endif';
terminal PRAGMA: 'pragma';

// special keywords
// ... see common
