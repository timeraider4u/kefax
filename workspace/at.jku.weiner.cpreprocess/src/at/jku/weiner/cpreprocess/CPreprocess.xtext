/*
 [The "BSD licence"]
 Copyright (c) 2013 Sam Harwell
 Copyright (c) 2015 Harald Weiner
 All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:
 1. Redistributions of source code must retain the above copyright
    notice, this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright
    notice, this list of conditions and the following disclaimer in the
    documentation and/or other materials provided with the distribution.
 3. The name of the author may not be used to endorse or promote products
    derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
grammar at.jku.weiner.cpreprocess.CPreprocess // with org.eclipse.xtext.common.Terminals
//hidden(LINEBREAK)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate cPreprocess "http://www.jku.at/weiner/cpreprocess/CPreprocess"

Model:
	{Model}
	(units+=TranslationUnit);
	
TranslationUnit:
	{TranslationUnit}
	(	lines+=PreprocessorDirectives
		|
		lines+=NewLineLine
		|	
		lines+=Code
	)*
;

PreprocessorDirectives returns SourceCodeLine:
	{PreprocessorDirectives}
	WS* HASH WS*
	(
			directive=IncludeDirective
		|	directive=DefineDirective
		|	directive=ErrorDirective
		|	directive=UnDefineDirective
		|	directive=PragmaDirective
	)
	NEWLINE
;

IncludeDirective:
	{IncludeDirective}
	INCLUDE WS+ string=MyCodeLine
;

DefineDirective:
	{DefineDirective}
	DEFINE WS+ id=ID WS* string=MyCodeLine
;

ErrorDirective:
	{ErrorDirective}
	ERROR WS+ msg=MyCodeLine
;

UnDefineDirective:
	{UnDefineDirective}
	UNDEF WS+ id=ID
;

PragmaDirective:
	{PragmaDirective}
	PRAGMA
;

NewLineLine returns SourceCodeLine:
	{NewLineLine}
	NEWLINE
;

Code returns SourceCodeLine:
	{Code}
	code=MyCodeLineExtended
	NEWLINE
;

MyCode returns ecore::EString:
	ID|SPECIAL|INCLUDE|DEFINE|ERROR|PRAGMA
;

MyCodeLine returns ecore::EString:
	(MyCode|WS)+
;

MyCodeLineExtended returns ecore::EString:
	WS* (MyCode (MyCodeLine|HASH)*)?
;


// lexer tokens
terminal HASH: '#';
terminal INCLUDE: 'include';
terminal DEFINE: 'define';
terminal ERROR: 'error';
terminal UNDEF: 'undef';
terminal PRAGMA: 'pragma';
terminal fragment LINEFEED: '\n';
terminal fragment CARRIAGERETURN: '\r';
terminal NEWLINE: (CARRIAGERETURN | LINEFEED) ;
terminal fragment BACKSLASH: '\\';
terminal fragment LINEBREAK: (BACKSLASH NEWLINE);
terminal fragment SPACE: ' ';
terminal fragment TAB: '\t';
terminal WS: (SPACE | TAB | LINEBREAK);

terminal ID:
	ID_NONDIGIT
		(	ID_NONDIGIT
		|	DIGIT
		)*
;
terminal fragment ID_NONDIGIT:
		NONDIGIT_LETTER
	|	UNIVERSAL_CHARACTER
;
terminal fragment DIGIT: '0' .. '9';
terminal fragment NONDIGIT_LETTER:
		'a' .. 'z'
	|	'A' .. 'Z'
;
terminal fragment
UNIVERSAL_CHARACTER:
		'\\u' HEX_QUAD
	|	'\\U' HEX_QUAD HEX_QUAD
	;
terminal fragment HEX_QUAD:
	HEXADECIMAL_DIGIT HEXADECIMAL_DIGIT
	HEXADECIMAL_DIGIT HEXADECIMAL_DIGIT
;
terminal fragment 
HEXADECIMAL_DIGIT:
		'0' .. '9'
	|	'a' .. 'f'
	|	'A' .. 'F'
	;

terminal SPECIAL: .;
