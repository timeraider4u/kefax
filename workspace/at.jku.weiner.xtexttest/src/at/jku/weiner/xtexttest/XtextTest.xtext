grammar at.jku.weiner.xtexttest.XtextTest with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate xtextTest "http://www.jku.at/weiner/xtexttest/XtextTest"

XtextTest:
	{XtextTest}
	PACKAGE package=PackageID
	LANGUAGE lang=LangID
	(IMPORTS imports=ImportID)?
	(SPLITLEXER ASSIGNASSINGLE boolean=Boolean)?
	input=Input
	(tokens=Tokens)?
	root=Element
	(output=Generator)?
	(before=Before)?
	(after=After)?
;

Boolean:
	TRUE | FALSE
;

PackageID returns ecore::EString:
	IDENTIFIER (POINT IDENTIFIER)*
;

LangID returns ecore::EString:
	IDENTIFIER
;

ImportID returns ecore::EString:
	IDENTIFIER (POINT IDENTIFIER)*
;

Input:
	{Input}
	SOURCE
	(SRCTEXT ASSIGNASSINGLE text=STRING
	|FILE ASSIGNASSINGLE file=STRING
	)
;

Tokens:
	{Tokens}
	LEXER
	(tokens+=MyTokens)+
;

MyTokens:
	token=IDENTIFIER 
	(ASSIGNASDATALIST count=INT)? 
;

Element:
	{Element}
	LEFTPAREN
		(importing=IDENTIFIER IMPORTER)?
		name=IDENTIFIER
		(
			(inner+=Inner)
			(COMMA inner+=Inner)*
		)?
	RIGHTPAREN
;

Inner:
	{Inner}
	
	parameter=IDENTIFIER
	(ASSIGNASSINGLE assign=Element
	|
	ASSIGNASLIST assignList+=Element (COMMA assignList+=Element)*
	|
	ASSIGNASSTRING value=STRING
	|
	ASSIGNASDATALIST assignAsData=STRING
	|
	ASSIGNASBOOL assignAsBool=(TRUE|FALSE)
	|
	isNull?=ISNULL
	|
	isNotNull?=ISNOTNULL
	)
;

Generator:
	{Generator}
	(OUTPUT FILE ASSIGNASSINGLE output=STRING)
	(EXPECTED FILE 
		((ASSIGNASSINGLE expected=STRING)
			| isSameAsInputFile?=ISSAMEASINPUTFILE
		)
	)
	(PATTERNS
		(FILE ASSIGNASSINGLE patternFile=STRING)?
		(replacePatterns+=ReplacePatterns)*
	)?
	(EXPECTED EXCEPTIONS
		exception=PackageID
	)?
;

ReplacePatterns:
	regex=STRING ASSIGNASSINGLE replace=STRING
;

Before:
	BEFORE_KW CLASS_KW ASSIGNASSINGLE myclass=PackageID
	METHOD_KW ASSIGNASSINGLE method=PackageID 
	LEFTPAREN RIGHTPAREN
;

After:
	AFTER_KW CLASS_KW ASSIGNASSINGLE myclass=PackageID
	METHOD_KW ASSIGNASSINGLE method=PackageID
	LEFTPAREN RIGHTPAREN
;

terminal PACKAGE: 'package';
terminal LANGUAGE: 'language';
terminal IMPORTS: 'imports';
terminal POINT: '.';
terminal SPLITLEXER: 'split-lexer';
terminal SOURCE: 'source';
terminal SRCTEXT: 'text';
terminal FILE: 'file';
terminal LEXER: 'lexer-tokens';
terminal OUTPUT: 'output';
terminal EXPECTED: 'expected';
terminal ISSAMEASINPUTFILE: 'isSameAsInputFile';
terminal PATTERNS: 'replacePatterns';
terminal EXCEPTIONS: 'exceptions';
terminal BEFORE_KW: 'before';
terminal AFTER_KW: 'after';
terminal CLASS_KW: 'class';
terminal METHOD_KW: 'method';
terminal ISNULL: 'isNull';
terminal ISNOTNULL: 'isNotNull';
terminal LEFTPAREN: '(';
terminal RIGHTPAREN: ')';
terminal ASSIGNASBOOL: '?=';
terminal TRUE: 'true';
terminal FALSE: 'false';
terminal ASSIGNASSINGLE: '=';
terminal ASSIGNASLIST: '+=';
terminal ASSIGNASSTRING: '++';
terminal ASSIGNASDATALIST: '+*';
terminal COMMA: ',';
terminal IDENTIFIER: ID;
terminal IMPORTER: '::';
