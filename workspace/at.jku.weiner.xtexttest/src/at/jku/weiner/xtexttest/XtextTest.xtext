grammar at.jku.weiner.xtexttest.XtextTest with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate xtextTest "http://www.jku.at/weiner/xtexttest/XtextTest"

XtextTest:
	{XtextTest}
	PACKAGE package=PackageID
	LANGUAGE lang=LangID
	input=Input
	(tokens=Tokens)?
	root=Element
	(output=Generator)?
	(before=Before)?
	(after=After)?
;

PackageID returns ecore::EString:
	IDENTIFIER (POINT IDENTIFIER)*
;

LangID returns ecore::EString:
	IDENTIFIER
;

Input:
	{Input}
	SOURCE
	(SRCTEXT ASSIGNASSINGLE text=STRING
	|FILE ASSIGNASSINGLE file=STRING
	)
;

Tokens:
	{Tokens}
	LEXER
	(tokens+=IDENTIFIER)+
;

Element:
	{Element}
	LEFTPAREN
		name=IDENTIFIER
		(
			(inner+=Inner)
			(COMMA inner+=Inner)*
		)?
	RIGHTPAREN
;

Inner:
	{Inner}
	parameter=IDENTIFIER
	(ASSIGNASSINGLE assign=Element
	|
	ASSIGNASLIST assignList+=Element (COMMA assignList+=Element)*
	|
	ASSIGNASSTRING value=STRING
	|
	ASSIGNASDATALIST assignAsData=STRING
	)
;

Generator:
	{Generator}
	(OUTPUT FILE ASSIGNASSINGLE output=STRING)
	(EXPECTED FILE 
		((ASSIGNASSINGLE expected=STRING)
			| isSameAsInputFile?=ISSAMEASINPUTFILE
		)
	)
	(PATTERNS
		(FILE ASSIGNASSINGLE patternFile=STRING)?
		(replacePatterns+=ReplacePatterns)*
	)?
	(EXPECTED EXCEPTIONS
		exception=PackageID
	)?
;

ReplacePatterns:
	regex=STRING ASSIGNASSINGLE replace=STRING
;

Before:
	BEFORE_KW CLASS_KW ASSIGNASSINGLE myclass=PackageID
	METHOD_KW ASSIGNASSINGLE method=PackageID 
	LEFTPAREN RIGHTPAREN
;

After:
	AFTER_KW CLASS_KW ASSIGNASSINGLE myclass=PackageID
	METHOD_KW ASSIGNASSINGLE method=PackageID
	LEFTPAREN RIGHTPAREN
;

terminal PACKAGE: 'package';
terminal LANGUAGE: 'language';
terminal POINT: '.';
terminal SOURCE: 'source';
terminal SRCTEXT: 'text';
terminal FILE: 'file';
terminal LEXER: 'lexer-tokens';
terminal OUTPUT: 'output';
terminal EXPECTED: 'expected';
terminal ISSAMEASINPUTFILE: 'isSameAsInputFile';
terminal PATTERNS: 'replacePatterns';
terminal EXCEPTIONS: 'exceptions';
terminal BEFORE_KW: 'before';
terminal AFTER_KW: 'after';
terminal CLASS_KW: 'class';
terminal METHOD_KW: 'method';
terminal LEFTPAREN: '(';
terminal RIGHTPAREN: ')';
terminal ASSIGNASSINGLE: '=';
terminal ASSIGNASLIST: '+=';
terminal ASSIGNASSTRING: '++';
terminal ASSIGNASDATALIST: '+*';
terminal COMMA: ',';
terminal IDENTIFIER: ID;
